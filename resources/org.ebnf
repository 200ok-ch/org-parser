S = line*

<line> = (empty-line / head-line / affiliated-keyword-line / todo-line / greater-block-begin-line / greater-block-end-line / content-line) eol

empty-line = "" | #"\s+"
content-line = #".*"

<eol> = <#'\n|$'>
<s> = <#"[\t ]+">
<word> = #"[^\r\n\s$]+"

head-line = stars [s priority] [s comment-token] s title [s tags]
stars = #'\*+'
priority = <"[#"> #"[A-Z]" <"]">
comment-token = <"COMMENT">
title = !tags word {s !tags word}
tags = <':'> ( tag <':'> )+
<tag> = #'[a-zA-Z0-9_@#%]+'

affiliated-keyword-line = <"#+"> (key | attr) <": "> value
optional = <"["> optional-value <"]">
value = #"[^\]\n]+"
<optional-value> = #"[^\]\n]+"
key = "HEADER" | "NAME" | "PLOT" | (("RESULTS" | "CAPTION") [ optional ]) | "AUTHOR" | "DATE" | "TITLE"
attr = <"ATTR_"> backend
backend = #"[a-zA-Z0-9-_]+"

todo-line = <"#+TODO: "> states
<states> = todo-state {s todo-state} <s "|" s> done-state {s done-state}
todo-state = #"[A-Z]+"
done-state = #"[A-Z]+"

greater-block-begin-line = <"#+BEGIN_"> greater-block-name [s greater-block-parameters]
greater-block-name = anything-but-whitespace
greater-block-parameters = anything-but-newline

<anything-but-whitespace> = #"[^\r\n\s$]+"
<anything-but-newline> = #"[^\n$]+"

greater-block-end-line = <"#+END_"> greater-block-name

drawer-begin-line = <":"> drawer-name <":">
drawer-name = #"[a-zA-Z0-9-_]+"
drawer-end-line = <":END:">

(* dynamic_block = '#+BEGIN:' s name [s parameters] contents '#+END:' *)
